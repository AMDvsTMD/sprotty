grammar io.typefox.sprotty.example.multicore.MulticoreAllocation with org.eclipse.xtext.common.Terminals

generate multicoreAllocation "http://www.typefox.io/sprotty/example/multicore/MulticoreAllocation"

Program:
	'program' name=ID ('for' numberOfCores=INT 'cores')?
	declarations+=Declaration*;

Declaration:
	Kernel | Task | Barrier;

Kernel:
	'kernel' name=ID ('{'
		('duration' '=' duration=INT)?
		('stackSize' '=' stackSize=INT)?
	'}')?;

Task:
	'task' name=ID ':' 'execute' kernel=[Kernel] ('on' 'core' core=INT)?;

Barrier:
	'join' joined+=[Task] (',' joined+=[Task])*
	('then' triggered+=Task (',' triggered+=Task)*)?;
