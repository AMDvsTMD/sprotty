grammar io.typefox.sprotty.example.multicore.MulticoreAllocation with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate multicoreAllocation "http://www.typefox.io/sprotty/example/multicore/MulticoreAllocation"

Schedule:
	'schedule' name=ID 'for' numberOfCores=INT 'cores'
	declarations+=Declaration*;

Declaration:
	Task | CoreAllocation;

Task:
	'task' name=ID ':' duration=INT 'ticks';

CoreAllocation:
	'core' selectors+=CoreSelector (',' selectors+=CoreSelector)* '{'
		statements+=Statement*
	'}';

CoreSelector:
	from=Integer ('..' to=Integer)?;

Statement:
	ExecuteStatement | LoopStatement | SendStatement | ReceiveStatement;

ExecuteStatement:
	'execute' task=[Task];

LoopStatement:
	{Loop}
	'loop' (times=Integer 'times')? '{'
		statements+=Statement*
	'}';

SendStatement:
	'send' 'to' direction=Direction;

ReceiveStatement:
	'receive' 'from' direction=Direction;

enum Direction:
	north | east | south | west;

Integer returns ecore::EIntegerObject:
	INT;
